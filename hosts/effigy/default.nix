# Do not modify this file!  It was generated by ‘nixos-generate-config’
# and may be overwritten by future invocations.  Please make changes
# to /etc/nixos/configuration.nix instead.
{
  inputs,
  config,
  lib,
  pkgs,
  modulesPath,
  ...
}:

{
  imports =
    with inputs;
    [
      (modulesPath + "/installer/scan/not-detected.nix")
      disko.nixosModules.disko
      sops-nix.nixosModules.sops
    ]
    ++ (with nixos-hardware.nixosModules; [
      common-cpu-intel
      common-gpu-nvidia
      common-pc-laptop
      common-pc-ssd
    ])
    ++ [
      ./disks.nix
    ];

  boot = {
    plymouth.enable = true;
    initrd = {
      availableKernelModules = [
        "xhci_pci"
        "ahci"
        "nvme"
        "sdhci_pci"
      ];
      kernelModules = [ "dm-snapshot" ];
    };
    kernelModules =
      if (lib.versionAtLeast config.boot.kernelPackages.kernel.version "6.1") then
        [
          "kvm-intel"
          "hp-wmi"
        ]
      else
        [
          "kvm-intel"
        ];
    kernelPackages = pkgs.linuxPackages_latest;
    kernelParams = [ "quiet" ];

    loader = {
      grub = {
        device = "nodev";
        efiSupport = true;
        useOSProber = true;
      };
      efi.canTouchEfiVariables = true;
    };

    tmp.useZram = true;
  };

  fileSystems = {
    "/mnt/users" = {
      device = "hub:/users";
      fsType = "nfs";
      options = [
        "x-systemd.mount-timeout=3s"
        "soft"
        "nofail"
        "noatime"
      ];
    };
    "/mnt/games" = {
      device = "hub:/games";
      fsType = "nfs";
      options = [
        "x-systemd.mount-timeout=3s"
        "soft"
        "nofail"
        "noatime"
      ];
    };
  };

  sops = {
    age.keyFile = "/root/.config/sops/age/keys.txt";
    secrets = {
      home = {
        format = "binary";
        sopsFile = ./home.conf;
      };
      proxy = {
        format = "binary";
        sopsFile = ./proxy.conf;
      };
    };
  };

  security = {
    pam.mount = {
      enable = true;
      createMountPoints = true;
      fuseMountOptions = [
        "nodev"
        "nosuid"
      ];
      additionalSearchPaths = with pkgs; [
        gocryptfs
      ];
    };
  };

  networking = {
    hostName = "effigy";
    networkmanager = {
      enable = true;
      wifi = {
        backend = "iwd";
        powersave = true;
      };
    };
    useDHCP = lib.mkDefault true;
    wg-quick.interfaces = {
      home = {
        autostart = false;
        configFile = config.sops.secrets.home.path;
      };
      proxy = {
        autostart = false;
        configFile = config.sops.secrets.proxy.path;
      };
    };
  };

  programs = {
    gnupg.agent.enable = true;
    nix-ld.enable = true;
    gamemode.enable = true;
    steam = {
      enable = true;
      extraCompatPackages = with pkgs; [
        proton-ge-bin
      ];
    };
    retrom.enable = true;
  };

  services = {
    greetd = {
      enable = true;
      useTextGreeter = true;
      greeterManagesPlymouth = true;
      settings = {
        default_session = {
          command = lib.getExe pkgs.tuigreet;
        };
      };
    };
    xserver.videoDrivers = [
      "nvidia"
      "intel"
    ];
    pipewire = {
      enable = true;
      jack.enable = true;
    };
    udisks2.enable = true;
    power-profiles-daemon.enable = true;
    upower.enable = true;
  };

  nixpkgs = {
    hostPlatform = lib.mkDefault "x86_64-linux";
    config.allowUnfree = true;
  };

  environment.systemPackages = with pkgs; [
    waypipe
  ];

  hardware = {
    cpu.intel.updateMicrocode = lib.mkDefault config.hardware.enableRedistributableFirmware;
    bluetooth.enable = true;
    graphics = {
      enable = true;
      enable32Bit = true;
    };
    nvidia = {
      open = true;
      modesetting.enable = true;
      nvidiaSettings = false;
      prime = {
        offload = {
          enable = true;
          enableOffloadCmd = true;
        };
        intelBusId = "PCI:0:2:0";
        nvidiaBusId = "PCI:1:0:0";
      };
      powerManagement = {
        enable = true;
        finegrained = true;
      };
    };
  };

  powerManagement = {
    enable = true;
    powertop.enable = true;
  };

  zramSwap.enable = true;

  stylix = {
    enable = true;
    base16Scheme = lib.mkDefault "${pkgs.base16-schemes}/share/themes/catppuccin-mocha.yaml";
  };
}
